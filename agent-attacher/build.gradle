/*
 * =========================================================================================
 * Copyright Â© 2013-2018 the kamon project <http://kamon.io/>
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 * =========================================================================================
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

dependencies {
    compile 'net.bytebuddy:byte-buddy-agent:1.8.9'
    testCompile 'org.mockito:mockito-core:2.7.22'
}

shadowJar {
    baseName = 'kanela-attacher'
    classifier = null

    exclude 'META-INF/services/javax.*'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    relocate 'io.vavr', 'kanela.agent.attacher.libs.io.vavr'
    relocate 'net.bytebuddy', 'kanela.agent.attacher.libs.net.bytebuddy'

    dependencies {
        exclude('org.projectlombok:lombok:1.16.20')
    }
}


jar {
    manifest {
        attributes 'Built-By': System.getProperty("user.name")
        attributes 'Build-Jdk': System.getProperty("java.version")
        attributes 'Main-Class': "kanela.agent.attacher.Kanela"
    }
}

jar.finalizedBy shadowJar

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id "dpsoft"
            name "Diego Parra"
            url "https://twitter.com/diegolparra"
        }

        developer {
            id "ivantopo"
            name "Ivan Topolnjak"
            url "https://twitter.com/ivantopo"
        }

        developer {
            id "cspinetta"
            name "Cristian Spinetta"
            url "https://twitter.com/c_spinetta"
        }
    }

    scm {
        url "https://github.com/kamon-io/kanela"
    }
}

publishing {
    publications {
        shadowPublication(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
            groupId 'io.kamon'
            artifactId 'kanela-agent-attacher'
            version "${version}"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Attacher for Kanela Agent')
                root.appendNode('name', 'Kanela Agent Attacher')
                root.appendNode('url', 'http://kamon.io')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['shadowPublication']

    pkg {
        repo = 'releases'
        name = 'kanela-attacher'
        userOrg = 'kamon-io'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kamon-io/kanela'

        version {
            name = "${version}"
            desc = 'Kanela Attacher'
            released  = new Date()
        }
    }
}
