/*
 * =========================================================================================
 * Copyright Â© 2013-2018 the kamon project <http://kamon.io/>
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 * =========================================================================================
 */


plugins {
    id 'java'
    id 'scala'
    id 'maven-publish'
    id 'signing'
    id 'com.github.maiflai.scalatest' version '0.32'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

configurations {
    kamonInstrumentationBundle
}

dependencies {
    implementation 'com.typesafe:config:1.4.2'
    implementation 'com.blogspot.mydailyjava:weak-lock-free:0.18'

    implementation 'org.tinylog:tinylog:1.3.6'
    implementation 'com.github.tobiasrm:tinylog-coloredconsole:1.3.1'
    implementation 'net.bytebuddy:byte-buddy-agent:1.11.18'

    implementation files('libs/byte-buddy-1.11.18.jar')

    testImplementation 'org.mockito:mockito-core:2.28.2' // 4.6.x
    testImplementation 'org.scalatest:scalatest_2.12:3.2.12'
    testImplementation "org.scala-lang:scala-library:${scala_version}"
    testImplementation 'org.pegdown:pegdown:1.6.0'
}

def agentBootstrapClasses = 'kanela/agent/bootstrap/**'

task bootstrapJar(type: Jar) {
    // Output to 'bootstrap.jar'.
    archiveBaseName = 'bootstrap'
    archiveVersion = null

    from sourceSets.main.output
    include agentBootstrapClasses
}

shadowJar.dependsOn bootstrapJar

shadowJar {
    archiveBaseName = 'kanela-agent'
    classifier = null

    mergeServiceFiles {
       path = 'META-INF/services/org.pmw.tinylog.*'
    }

    exclude 'META-INF/services/javax.*'
    exclude 'META-INF/licenses/**'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/*.RSA'
    exclude 'tinylog.properties'

    //exclude agentBootstrapClasses

    relocate 'net.bytebuddy', 'kanela.agent.libs.net.bytebuddy'
    relocate 'io.vavr', 'kanela.agent.libs.io.vavr'
    relocate 'com.typesafe.config', 'kanela.agent.libs.com.typesafe.config'
    relocate 'org.pmw.tinylog', 'kanela.agent.libs.org.pmw.tinylog'
    relocate 'com.blogspot.mydailyjava', 'kanela.agent.libs.com.blogspot.mydailyjava'

    relocate ('com.github.tobiasrm', 'kanela.agent.libs.com.github.tobiasrm') {
        exclude 'com.github.tobiasrm.Main.class'
    }

    dependencies {
        exclude('org.projectlombok:lombok:1.18.24')
    }

    doLast {
        def agentBootstrapJar = 'kanela/agent/bootstrap.jar'

        // Bundle bootstrap.jar.
        ant.jar(update: 'true', destfile: shadowJar.archivePath) {
            mappedresources {
                fileset(file: bootstrapJar.archivePath)
                globmapper(from: '*', to: agentBootstrapJar)
            }
        }
    }
}

jar {
    manifest {
        attributes 'Built-By': System.getProperty("user.name")
        attributes 'Build-Jdk': System.getProperty("java.version")
        attributes 'Premain-Class': "kanela.agent.Kanela"
        attributes 'Agent-Class': "kanela.agent.Kanela"
        attributes 'Main-Class': "kanela.agent.attacher.Attacher"
        attributes 'Can-Redefine-Classes': "true"
        attributes 'Can-Set-Native-Method-Prefix': "true"
        attributes 'Can-Retransform-Classes': "true"
    }
}

jar.finalizedBy shadowJar

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "https://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id "dpsoft"
            name "Diego Parra"
            url "https://twitter.com/diegolparra"
        }

        developer {
            id "ivantopo"
            name "Ivan Topolnjak"
            url "https://twitter.com/ivantopo"
        }

        developer {
            id "cspinetta"
            name "Cristian Spinetta"
            url "https://twitter.com/c_spinetta"
        }
    }

    scm {
       url "https://github.com/kamon-io/kanela"
    }
}

publishing {
    publications {
        shadowPublication(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
            groupId 'io.kamon'
            artifactId 'kanela-agent'
            version "${project.version}"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'The Kamon Instrumentation Agent')
                root.appendNode('name', 'Kanela')
                root.appendNode('url', 'https://kamon.io/')
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            credentials {
                def user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('SONATYPE_USER')
                def pass = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('SONATYPE_PASS')

                username user as String
                password pass as String
            }

            def releasesRepoUrl =  'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }

    }
}

tasks.withType(Sign) {
    onlyIf { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish")  }
}

signing {
    sign publishing.publications.shadowPublication
}

task createBuildInfoFile(dependsOn: processResources) {
    doLast {
        def file = new File("$buildDir/resources/main/build-info.properties")
        Properties props = new Properties()
        props.setProperty('version', project.version.toString())
        props.setProperty('timestamp', new Date().toString())
        props.store file.newWriter(), null
    }
}

classes {
    dependsOn createBuildInfoFile
}

test.reports.html.enabled = false
